ext {
  minJavaVersionForTests = JavaVersion.VERSION_11
}

muzzle {
  pass {
    group = "org.eclipse.jetty"
    module = 'jetty-server'
    versions = "[11,12)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

sourceSets {
  "main_java11" {
    java.srcDirs "${project.projectDir}/src/main/java11"
  }
}

compileMain_java11Java.doFirst {
  if (!System.env.JAVA_11_HOME) {
    throw new GradleException('JAVA_11_HOME must be set to build Java 11 code')
  }
  options.fork = true
  options.forkOptions.javaHome = file(System.env.JAVA_11_HOME)
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
  main_java11CompileOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: '11.0.0'

  // Don't want to conflict with jetty from the test server.
  testImplementation(project(':dd-java-agent:testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '11.0.0'

  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '11.+'
}
