muzzle {
  pass {
    group = "javax.servlet"
    module = 'javax.servlet-api'
    versions = "[,]"
  }
  pass {
    group = "javax.servlet"
    module = 'servlet-api'
    versions = "[,]"
    skipVersions += '0'
  }
}

apply from: "$rootDir/gradle/java.gradle"

sourceSets {
  common {
    compileClasspath = sourceSets.main.compileClasspath
    java {
      srcDir 'src/common/java'
    }
  }
  test {
    compileClasspath += common.output
    runtimeClasspath += common.output
  }
}

// to be used a compile-only dependency
task commonJar(type: Jar) {
  archiveClassifier = 'common'
  from sourceSets.common.output
}

jar {
  from sourceSets.common.output
}

dependencies {
  compileOnly group: 'javax.servlet', name: 'servlet-api', version: '2.3'

  testImplementation group: 'javax.servlet', name: 'servlet-api', version: '2.3'

  // servlet request instrumentation required for linking request to response.
  testImplementation project(':dd-java-agent:instrumentation:servlet:request-2')

  // Don't want to conflict with jetty from the test server.
  testImplementation(project(':dd-java-agent:testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }

  testImplementation project(':dd-java-agent:instrumentation:jetty-util')
}
