ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"
description = 'SpringBoot Open Liberty Smoke Tests'

spotless {
  groovy {
    excludeJava()
    greclipse().configFile(project.rootProject.rootDir.path + '/gradle/enforcement/spotless-groovy.properties')
  }
}

dependencies {
  testCompile project(':dd-smoke-tests')
}

def appDir = "$projectDir/application"
def targetDir = "$buildDir/openliberty"
def jarName = "demo-open-liberty-app.jar"

// compile the Open liberty spring boot server
task mvnStage(type:Exec) {
  workingDir "$appDir"
  commandLine './mvnw', 'package'
  copy {
    from file("$appDir/target")
    into file("$targetDir")
  }
}

compileTestGroovy {
  dependsOn 'mvnStage'
  outputs.upToDateWhen {
    !mvnStage.didWork
  }
}

//add the embedded server jar into the class path
tasks.withType(Test).configureEach {
  jvmArgs "-Ddatadog.smoketest.openliberty.jar.path=${targetDir}/$jarName"
}

spotless {
  java {
    target fileTree("$appDir") {
      include "**/*.java"
    }
  }

  groovyGradle {
    target '*.gradle', "**/*.gradle"
  }
}
